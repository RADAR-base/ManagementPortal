import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'maven-publish'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'org.jetbrains.dokka'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

description = 'Library for authentication and authorization of JWT tokens issued by the RADAR platform'

dependencies {
    api("com.auth0:java-jwt:${oauth_jwt_version}") {
        constraints {
            implementation("com.fasterxml.jackson.core:jackson-databind:${jackson_version}") {
                because("Software vulnerability exists in lower versions")
            }
        }
    }
    api(platform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:$coroutines_version"))
    api("org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version")

    implementation("org.radarbase:radar-commons-kotlin:$radar_commons_version")

    implementation(platform("io.ktor:ktor-bom:$ktor_version"))
    implementation("io.ktor:ktor-client-core:$ktor_version")
    implementation("io.ktor:ktor-client-cio:$ktor_version")
    implementation("io.ktor:ktor-client-content-negotiation:$ktor_version")
    implementation("io.ktor:ktor-serialization-kotlinx-json:$ktor_version")

    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j_version

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit_version
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: wiremock_version
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: hamcrest_version

    testRuntimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: logback_version
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit_version

}

tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_11
        apiVersion = KotlinVersion.KOTLIN_1_8
        languageVersion = KotlinVersion.KOTLIN_1_8
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
    useJUnitPlatform()
}

tasks.register('ghPagesJavadoc', Copy) {
    from file("$buildDir/dokka/javadoc")
    into file("$rootDir/public/radar-auth-javadoc")
    dependsOn(dokkaJavadoc)
}

ext.projectLanguage = "kotlin"

apply from: "$rootDir/gradle/style.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
