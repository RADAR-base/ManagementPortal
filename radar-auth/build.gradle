plugins {
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'

ext.jacksonVersion = '2.9.8'
ext.okhttpVersion = '3.14.0'
ext.oauthJwtVersion = '3.7.0'

description = 'Library for authentication and authorization of JWT tokens issued by the RADAR platform'

repositories {
    jcenter()
    mavenLocal()
}

dependencies {
    implementation project(path: ':radar-auth:deprecated-auth0', configuration: 'shadow')
    api group: 'com.auth0', name: 'java-jwt', version: oauthJwtVersion
    implementation group: 'com.squareup.okhttp3', name: 'okhttp', version: okhttpVersion

    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.26'
    implementation group: 'com.fasterxml.jackson.core' , name: 'jackson-databind' , version: jacksonVersion
    implementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: jacksonVersion

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version: '2.21.0'
    testCompile group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'


    testRuntimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
}

shadowJar {
    configurations = [project.configurations.shadow]
}


def sharedManifest = manifest {
    attributes("Implementation-Title": project.name,
            "Implementation-Version": version)
}

jar {
//    archiveBaseName.set(project.name)
    manifest.from sharedManifest
}

// custom tasks for creating source/javadoc jars
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
    manifest.from sharedManifest
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    manifest.from sharedManifest
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar, javadocJar
}

publishing {
    publications {
        mavenJar(MavenPublication) {
            publication -> project.shadow.component(publication)
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = project.name
                description = project.description
                url = githubUrl
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'dennyverbeeck'
                        name = 'Denny Verbeeck'
                        email = 'dverbeec@its.jnj.com'
                        organization = 'Janssen R&D'
                    }
                    developer {
                        id = 'blootsvoets'
                        name = 'Joris Borgdorff'
                        email = 'joris@thehyve.nl'
                        organization = 'The Hyve'
                    }
                    developer {
                        id = 'nivemaham'
                        name = 'Nivethika Mahasivam'
                        email = 'nivethika@thehyve.nl'
                        organization = 'The Hyve'
                    }
                }
                issueManagement {
                    system = 'GitHub'
                    url = githubUrl + '/issues'
                }
                organization {
                    name = 'RADAR-base'
                    url = website
                }
                scm {
                    connection = 'scm:git:' + githubUrl
                    url = githubUrl
                }
            }

        }
    }
}

bintray {
    user project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    override false
    publications 'mavenJar'
    pkg {
        repo = project.group
        name = project.name
        userOrg = 'radar-cns'
        desc = project.description
        licenses = ['Apache-2.0']
        websiteUrl = website
        issueTrackerUrl = issueUrl
        vcsUrl = githubUrl
        githubRepo = githubRepoName
        githubReleaseNotesFile = 'README.md'
        version {
            name = project.version
            desc = project.description
            vcsTag = System.getenv('TRAVIS_TAG')
            released = new Date()
        }
    }
}


artifactory {
    contextUrl = 'https://oss.jfrog.org/artifactory'
    publish {
        repository {
            repoKey = 'oss-snapshot-local'
            username = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
            password = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
            maven = true
        }
        defaults {
            publishArtifacts = true
            publishPom = true
            properties = ['qa.level': 'basic']
        }
    }
}

artifactoryPublish {
    publications('mavenJar')
    dependsOn 'assemble'
}

bintrayUpload.dependsOn 'assemble'

apply from: '../gradle/style.gradle'
