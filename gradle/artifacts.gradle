
def sharedManifest = manifest {
    attributes("Implementation-Title": project.name,
            "Implementation-Version": version)
}

jar {
    manifest.from sharedManifest
}

// custom tasks for creating source/javadoc jars
tasks.register('sourcesJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
    manifest.from sharedManifest
    dependsOn(classes)
}

if (!project.hasProperty("projectLanguage") || projectLanguage == "java") {
    tasks.register('javadocJar', Jar) {
        archiveClassifier.set('javadoc')
        from javadoc.destinationDir
        manifest.from sharedManifest
        dependsOn(javadoc)
    }
} else if (projectLanguage == "kotlin") {
    tasks.register('javadocJar', Jar) {
        from("$buildDir/dokka/javadoc")
        archiveClassifier.set("javadoc")
        manifest.from sharedManifest
        dependsOn(dokkaJavadoc)
    }
}

assemble.dependsOn(javadocJar, sourcesJar)
