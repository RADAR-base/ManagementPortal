plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
}

apply plugin: 'maven-publish'

ext {
    jacksonVersion = '2.11.4'
    jacksonYamlVersion = '2.11.4'
    okhttpVersion = '4.9.1'
    oauthJwtVersion = '3.18.2'
    commonsCodecVersion = '1.15'
    slf4jVersion = '1.7.30'
    junitVersion ='5.6.3'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

description = 'Library for authentication and authorization of JWT tokens issued by the RADAR platform'

repositories {
    mavenCentral()
}

configurations {
    testImplementation.extendsFrom shadow
}

dependencies {
    implementation project(path: ':radar-auth:deprecated-auth0', configuration: 'shadow')
    shadow "commons-codec:commons-codec:$commonsCodecVersion"

    shadow group: 'com.auth0', name: 'java-jwt', version: oauthJwtVersion
    shadow group: 'com.squareup.okhttp3', name: 'okhttp', version: okhttpVersion

    shadow group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    shadow group: 'com.fasterxml.jackson.core' , name: 'jackson-databind' , version: jacksonVersion
    shadow group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-yaml', version: jacksonYamlVersion

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junitVersion
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.27.2'
    testImplementation group: 'com.github.stefanbirkner', name: 'system-rules', version: '1.19.0'

    testRuntimeOnly group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion

}

shadowJar {
    archiveClassifier.set(null)
    conventionMapping.with {
        map('classifier') {
            null
        }
    }
}

jar {
    enabled = false
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
    useJUnitPlatform()
}

task ghPagesJavadoc(type: Copy, dependsOn: javadoc) {
    from javadoc.destinationDir
    into file("$rootDir/public/radar-auth-javadoc")
}

ext.isShadowed = true
ext.projectLanguage = "java"

apply from: "$rootDir/gradle/style.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
