import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${spring_boot_version}")
    }
}

plugins {
    id 'application'
    id 'org.springframework.boot' version "${spring_boot_version}"
    id "com.github.node-gradle.node" version "3.6.0"
    id "io.spring.dependency-management" version "1.1.3"
    id 'de.undercouch.download' version '5.5.0' apply false
    id "io.github.gradle-nexus.publish-plugin" version "1.3.0"
    id("com.github.ben-manes.versions") version "0.47.0"
    id 'org.jetbrains.kotlin.jvm' version "1.9.22"
    id "org.jetbrains.kotlin.kapt" version "1.9.22"
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.9.22' apply false
    id 'org.jetbrains.dokka' version "1.8.20"
    id "org.jetbrains.kotlin.plugin.allopen" version "1.9.10"
    id "io.sentry.jvm.gradle" version "4.11.0"
}

apply plugin: 'org.springframework.boot'
apply plugin: 'war'
apply plugin: 'com.github.node-gradle.node'
apply plugin: 'io.spring.dependency-management'

allprojects {
    group 'org.radarbase'
    version '2.1.9-SNAPSHOT' // project version

    // The comment on the previous line is only there to identify the project version line easily
    // with a sed command, to auto-update the version number with the prepare-release-branch.sh
    // script, do not remove it.

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'idea'

    ext.githubRepoName = 'RADAR-base/ManagementPortal'
    ext.githubUrl = 'https://github.com/RADAR-base/ManagementPortal'
    ext.website = 'https://radar-base.org'

    repositories {
        mavenCentral()

        // This repo is used temporarily when we use an internal SNAPSHOT library version.
        // When we publish the library, we comment out the repository again to speed up dependency resolution.
//        maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    idea {
        module {
            downloadSources = true
        }
    }

    tasks.register("ghPages") {
        dependsOn(provider {
            tasks.findAll { task -> task.name.startsWith("ghPages") && task.name != "ghPages" }
        })
    }
}

description = 'MangementPortal application to manage studies and participants'

defaultTasks 'bootRun'

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
}

application {
    mainClass.set('org.radarbase.management.ManagementPortalApp')
    applicationDefaultJvmArgs = [
            '--add-modules', 'java.se',
            '--add-exports', 'java.base/jdk.internal.ref=ALL-UNNAMED',
            '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
            '--add-opens', 'java.base/java.nio=ALL-UNNAMED',
            '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
            '--add-opens', 'java.management/sun.management=ALL-UNNAMED',
            '--add-opens', 'jdk.management/com.sun.management.internal=ALL-UNNAMED',
    ]
}

bootWar {
    launchScript()
}

springBoot {
    buildInfo()
}


bootRun {
    sourceResources sourceSets.main
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_17
        apiVersion = KotlinVersion.KOTLIN_1_8
        languageVersion = KotlinVersion.KOTLIN_1_8
    }
}

test {
    jvmArgs = [
            '--add-modules', 'java.se',
            '--add-exports', 'java.base/jdk.internal.ref=ALL-UNNAMED',
            '--add-opens', 'java.base/java.lang=ALL-UNNAMED',
            '--add-opens', 'java.base/java.nio=ALL-UNNAMED',
            '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
            '--add-opens', 'java.management/sun.management=ALL-UNNAMED',
            '--add-opens', 'jdk.management/com.sun.management.internal=ALL-UNNAMED',
    ]
    testLogging {
        exceptionFormat = 'full'
    }
    useJUnitPlatform()
}

apply from: 'gradle/liquibase.gradle'
apply from: 'gradle/gatling.gradle'
apply from: 'gradle/mapstruct.gradle'
apply from: 'gradle/docker.gradle'
apply from: 'gradle/style.gradle'
apply from: 'gradle/openapi.gradle'

if (project.hasProperty('prod')) {
    apply from: 'gradle/profile_prod.gradle'
} else {
    apply from: 'gradle/profile_dev.gradle'
}

ext.findbugAnnotationVersion = '3.0.2'
/* The entries in the block below are added here to force the version of
*  transitive dependencies and mitigate reported vulnerabilities */
ext['jackson.version'] = "${jackson_version}"
ext['undertow.version'] = "${undertow_version}"
ext['spring-security.version'] = "${spring_security_version}"
ext['logback.version'] = "1.2.13" // Cannot upgrade this to 1.3.x or 1.4.x; causes Scala error.

dependencies {

    /* The entries in the block below are added here to force the version of
    *  transitive dependencies and mitigate reported vulnerabilities */
    implementation("org.yaml:snakeyaml:${snakeyaml_version}")
    implementation('org.springframework.security:spring-security-web:5.7.13')

    implementation("org.jetbrains.kotlin:kotlin-reflect:${kotlin_version}")
    implementation("tech.jhipster:jhipster-framework:${jhipster_server_version}")
    implementation("tech.jhipster:jhipster-dependencies:${jhipster_server_version}")
    implementation("io.micrometer:micrometer-core:${micrometer_version}")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-reactor:$coroutines_version")
    implementation(platform("io.ktor:ktor-bom:$ktor_version"))
    implementation("io.ktor:ktor-client-core")
    implementation("io.ktor:ktor-client-cio")
    implementation("io.ktor:ktor-client-content-negotiation")
    implementation("io.ktor:ktor-serialization-kotlinx-json")
    implementation("com.fasterxml.jackson.core:jackson-core:${jackson_version}")
    runtimeOnly("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:${jackson_version}")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-hibernate5:${jackson_version}")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-csv:${jackson_version}")
    implementation "com.fasterxml.jackson.core:jackson-annotations:${jackson_version}"
    implementation("com.fasterxml.jackson.core:jackson-databind:${jackson_version}")
    implementation "com.hazelcast:hazelcast:${hazelcast_version}"
    implementation "com.hazelcast:hazelcast-spring:${hazelcast_version}"
    runtimeOnly "com.hazelcast:hazelcast-hibernate53:${hazelcast_hibernate_version}"
    runtimeOnly("com.zaxxer:HikariCP:${hikaricp_version}")
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: findbugAnnotationVersion
    implementation("org.liquibase:liquibase-core:${liquibase_version}")
    runtimeOnly "com.mattbertolini:liquibase-slf4j:${liquibase_slf4j_version}"
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-autoconfigure")
    implementation "org.springframework.boot:spring-boot-starter-mail"
    runtimeOnly "org.springframework.boot:spring-boot-starter-logging"
    runtimeOnly("org.springframework.boot:spring-boot-starter-data-jpa") {
        exclude group: 'org.hibernate', module: 'hibernate-entitymanager'
    }
    implementation "org.springframework.security:spring-security-data"

    implementation("org.springframework.boot:spring-boot-starter-web") {
        exclude module: 'spring-boot-starter-tomcat'
    }
    runtimeOnly "org.springframework.boot:spring-boot-starter-security"
    implementation("org.springframework.boot:spring-boot-starter-undertow")

    implementation "org.hibernate:hibernate-core"
    implementation "org.hibernate:hibernate-envers"
    implementation "org.hibernate:hibernate-validator:${hibernate_validator_version}"

    runtimeOnly "org.postgresql:postgresql:${postgresql_version}"
    runtimeOnly "org.hsqldb:hsqldb:${hsqldb_version}"

    // Fix vulnerabilities
    runtimeOnly("io.undertow:undertow-websockets-jsr")
    runtimeOnly("io.undertow:undertow-servlet")
    runtimeOnly("io.undertow:undertow-core")

    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    runtimeOnly("org.thymeleaf:thymeleaf:${thymeleaf_version}")
    runtimeOnly("org.thymeleaf:thymeleaf-spring5:${thymeleaf_version}")
    implementation("org.springframework:spring-context-support")
    implementation("org.springframework.session:spring-session-hazelcast")

    implementation('org.springframework.security.oauth:spring-security-oauth2:2.5.2.RELEASE')
    implementation "org.springdoc:springdoc-openapi-ui:${springdoc_version}"
    runtimeOnly("javax.inject:javax.inject:1")
    implementation project(':radar-auth')
    implementation "org.springframework.data:spring-data-envers"

    implementation "org.mockito:mockito-core:$mockito_version"
    implementation "org.mockito.kotlin:mockito-kotlin:${mockito_kotlin_version}"

    runtimeOnly("jakarta.xml.bind:jakarta.xml.bind-api:${javax_xml_bind_version}")
    runtimeOnly("org.glassfish.jaxb:jaxb-core:${javax_jaxb_core_version}")
    runtimeOnly("org.glassfish.jaxb:jaxb-runtime:${javax_jaxb_runtime_version}")
    runtimeOnly("javax.activation:activation:${javax_activation}")
    runtimeOnly 'org.javassist:javassist:3.29.2-GA'

    testImplementation "com.jayway.jsonpath:json-path"
    testImplementation ("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "com.vaadin.external.google", module:"android-json"
        exclude group: "junit", module: "junit"
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }
    testImplementation "org.springframework.security:spring-security-test"
    testImplementation "org.springframework.boot:spring-boot-test"
    testImplementation "org.assertj:assertj-core:${assertj_version}"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.mockito.kotlin:mockito-kotlin:${mockito_kotlin_version}"
    testImplementation "com.mattbertolini:liquibase-slf4j:${liquibase_slf4j_version}"
    testImplementation "org.hamcrest:hamcrest-library"
    testImplementation "org.testcontainers:testcontainers:${testcontainers_version}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"

    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
}

allOpen {
    annotation("org.springframework.stereotype.Component")
    annotation("org.springframework.boot.test.context.SpringBootTest")
    annotation("org.springframework.web.bind.annotation.RestController")
}

dependencyManagement {
    imports {
        mavenBom "com.fasterxml.jackson:jackson-bom:$jackson_version"
        mavenBom "org.springframework:spring-framework-bom:$spring_framework_version"
        mavenBom "org.springframework.boot:spring-boot-dependencies:$spring_boot_version"
        mavenBom "org.springframework.data:spring-data-bom:$spring_data_version"
        mavenBom "org.springframework.session:spring-session-bom:$spring_session_version"
    }
}

clean {
    delete "target"
}

tasks.register('cleanResources', Delete) {
    delete 'build/resources'
}

wrapper {
    gradleVersion '8.8'
}

tasks.register('stage') {
    dependsOn 'bootWar'
}

tasks.register('ghPagesJavadoc', Copy) {
    from javadoc.destinationDir
    into file("$rootDir/public/management-portal-javadoc")
    dependsOn tasks.named('javadoc')
}

tasks.register('ghPagesOpenApiSpec', Copy) {
    from file(layout.buildDirectory.dir("swagger-spec"))
    into file("$rootDir/public/apidoc")
}

compileJava.dependsOn processResources
processResources.dependsOn cleanResources, bootBuildInfo
bootBuildInfo.mustRunAfter cleanResources

tasks.register('downloadDependencies') {
    description "Pre-downloads dependencies"
    configurations.compileClasspath.files
    configurations.runtimeClasspath.files
}

ext.projectLanguage = "java"
apply from: 'gradle/artifacts.gradle'

nexusPublishing {
    repositories {
        sonatype {
            username = project.hasProperty("ossrh.user") ? project.property("ossrh.user") : System.getenv("OSSRH_USER")
            password = project.hasProperty("ossrh.password") ? project.property("ossrh.password") : System.getenv("OSSRH_PASSWORD")
        }
    }
}

def isNonStable = { String version ->
    def stableKeyword = ["RELEASE", "FINAL", "GA"].any { version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    doFirst {
        allprojects {
            repositories.removeAll {
                it instanceof MavenArtifactRepository && it.url.toString().contains("snapshot")
            }
        }
    }
    rejectVersionIf {
        it.currentVersion.split('\\.')[0] != it.candidate.version.split('\\.')[0]
            || isNonStable(it.candidate.version)
    }
}

node {
    download = true
}
