apply plugin: 'org.springframework.boot'
apply plugin: 'com.github.node-gradle.node'

ext.logbackLoglevel = 'INFO'

def profiles = 'prod'

if (project.hasProperty('no-liquibase')) {
    profiles += ',no-liquibase'
}

if (project.hasProperty('api-docs')) {
    profiles += ',api-docs'
}

if (project.hasProperty('e2e-prod-test')) {
    profiles += ',e2e-prod-test'
}

bootRun {
    args = []
    systemProperty "spring.profiles.active", profiles
}

task setEnvVars {
    doLast {
        def kratosUrl = System.getenv('KRATOS_URL')
        def baseUrl = System.getenv('BASE_URL')
        def envProdFileContent = """
        export const environment = {
          production: true,
          KRATOS_URL: '${kratosUrl}',
          BASE_URL: '${baseUrl}'
        };
        """
        file('src/main/webapp/environments/environment.prod.ts').text = envProdFileContent
    }
}

task webpack(type: YarnTask, dependsOn: 'yarn_install') {
    dependsOn setEnvVars
    args = ["build:prod"]
}

war {
    webAppDirName = 'build/www/'
}

processResources {
    filesMatching('**/logback-spring.xml') {
        filter {
            it.replace('#logback.loglevel#', logbackLoglevel)
        }
    }
    filesMatching('**/application.yml') {
        filter {
            it.replace('#spring.profiles.active#', profiles)
        }
        filter {
            it.replace('#project.version#', version.toString())
        }
    }
}

processResources.dependsOn webpack
