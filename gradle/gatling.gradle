apply plugin: 'scala'

ext.gatlingSrc = "${project.rootDir.absolutePath}/src/gatling"
ext.gatlingSimulations = "${gatlingSrc}/scala"

sourceSets {
    gatling {
        scala {
            srcDirs = [gatlingSimulations]
        }
    }
}

dependencies {
    gatlingImplementation "io.gatling.highcharts:gatling-charts-highcharts:${libs.versions.gatling}"
}

//noinspection GroovyAssignabilityCheck
tasks.withType(ScalaCompile) {
    scalaClasspath = scalaRuntime.inferScalaClasspath(configurations.gatlingCompileClasspath)
}

task manifestJar(type: Jar) {
    from configurations.gatlingRuntimeClasspath
    archiveFileName.set 'gatlingBooter.jar'
    doFirst {
        manifest {
            // uri is just needed for Windows-compatibility
            attributes 'Class-Path': configurations.gatlingCompileClasspath.files.collect{ project.uri(it) }.join(' ')
        }
    }
}

task gatlingRunAll(dependsOn: manifestJar) {
    group = 'gatling'
    description = 'Run all available Gatling simulations.'

    fileTree(dir: gatlingSimulations, include: '**/*Test.scala').each {
        def gatlingClass = it.name.replaceFirst(".scala", "")
        gatlingRunAll.dependsOn "gatlingRun${gatlingClass}"
    }
}

tasks.addRule("Pattern: gatlingRun<ID>") { String taskName ->
    if (taskName.startsWith("gatlingRun") && taskName != "gatlingRunAll") {
        task(taskName, type: JavaExec, dependsOn: manifestJar) {
            standardInput = System.in

            final def sourceSet = sourceSets.gatling

            classpath sourceSet.output + files(manifestJar.archiveFile) + files("${gatlingSrc}/resources")
            mainClass.set("io.gatling.app.Gatling")

            environment GATLING_HOME:''

            def className = taskName - 'gatlingRun'

            args '-rf', "${project.buildDir.absolutePath}/reports/gatling"
            args '-sf', gatlingSimulations
            args '-s', className
        }
    }
}
