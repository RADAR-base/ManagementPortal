import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.dsl.KotlinVersion
import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

/*
 * Copyright (c) 2020. The Hyve
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 *
 * See the file LICENSE in the root of this repository.
 */
plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'org.jetbrains.dokka'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_17


description = "Kotlin ManagementPortal client"

dependencies {
    api("org.jetbrains.kotlin:kotlin-stdlib:1.9.10")
    implementation("org.jetbrains.kotlin:kotlin-reflect:1.9.10")
    implementation("org.radarbase:radar-commons-kotlin:1.2.2")

    api(platform("org.jetbrains.kotlinx:kotlinx-coroutines-bom:$coroutines_version"))
    api(platform("io.ktor:ktor-bom:$ktor_version"))

    api("io.ktor:ktor-client-core")
    api("io.ktor:ktor-client-auth")
    implementation("io.ktor:ktor-client-cio")
    implementation("io.ktor:ktor-client-content-negotiation")
    implementation("io.ktor:ktor-serialization-kotlinx-json")

    testImplementation("org.jetbrains.kotlinx:kotlinx-coroutines-test")
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: junit_version
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version: '2.27.2'
    testImplementation group: 'org.mockito.kotlin', name: 'mockito-kotlin', version: mockito_kotlin_version
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'

    testRuntimeOnly group: 'org.slf4j', name: 'slf4j-simple', version: slf4j_version
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junit_version
}

tasks.withType(KotlinCompile).configureEach {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_17
        apiVersion = KotlinVersion.KOTLIN_1_7
        languageVersion = KotlinVersion.KOTLIN_1_9
    }
}

tasks.register('ghPagesJavadoc', Copy) {
    from file("$buildDir/dokka/javadoc")
    into file("$rootDir/public/managementportal-client-javadoc")
    dependsOn(dokkaJavadoc)
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
    useJUnitPlatform()
}

ext.projectLanguage = "kotlin"
ext.publishToMavenCentral = true

apply from: "$rootDir/gradle/publishing.gradle"
